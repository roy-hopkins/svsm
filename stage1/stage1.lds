/* SPDX-License-Identifier: MIT OR Apache-2.0 */

/*
 * Copyright (c) 2022-2023 SUSE LLC
 *
 * Author: Joerg Roedel <jroedel@suse.de>
 */

OUTPUT_ARCH(i386:x86-64)

SECTIONS
{
	.stext = ALIGN(.sdata  - SIZEOF(.text) - 4095, 4096);
	. = .stext;
	.text : { *(.startup.*) *(.text) *(.text.*) }

	.sdata = ALIGN(.srodata - SIZEOF(.data) - 4095, 4096);
	. = .sdata;
	.data : { *(.data) }

	.srodata = ALIGN(.sbss - SIZEOF(.rodata) - 4095, 4096);
	. = .srodata;
	.rodata : { *(.rodata) }

	/*
	 * Subtract an extra byte so that edata and the .init function
	 * symbols don't alias, which would be confusing when
	 * examining the resulting binary.
	 */
	.sbss = ALIGN(.sinit - SIZEOF(.bss) - 4095 - 1, 4096);
	. = .sbss;
	.bss . : { *(.bss) }
	edata = .;

	. = 0xfffff000;
	.sinit = .;
	.init . : {
		/*
		 * OVMF GDT is expected to be at 0xfffff000
		 */
		*(.ovmfgdt);
		. = 4096 - 512;
		*(.init);
		. = 4096 - 32 - 256;
		*(.sevmeta);
		. = 4096 - 16;
		*(.resetvector);
		. = 4096;
	}
	einit = .;

	/DISCARD/ : {*(.*)}
}

ENTRY(startup_32)
